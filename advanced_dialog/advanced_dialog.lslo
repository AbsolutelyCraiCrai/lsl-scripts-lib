/**
 * Advanced Dialog by John Parker
 * Version 1.4
 *
 * This script allows you to show "fancy" looking dialogs in LSL, including a title
 * icon and spaced-out button layouts.
 *
 * See readme file for usage instructions.
 *
 * Change history:
 *
 *    22/11/2019 - Added llTextBox() support with new "type" JSON parameter. Bumped
 *                 version to 1.1.
 *
 *    25/11/2019 - Added the ability to specify JSON_NULL as a button, which is
 *                 translated to a blank button should the scripter require it.
 *                 Bumped version to 1.2.
 *
 *    28/11/2019 - Fixed a small bug where setting a blank button list resulted in
 *                 a script error. The list length check is now done BEFORE null
 *                 parsing. Additionally, setting the list to one JSON_NULL value
 *                 now results in the standard OK button set to be used. Bumped
 *                 version to 1.3.
 *
 *    07/12/2019 - Show a script error if an invalid dialog type is specified.
 *                 Also, error messages are now specific to the problem, rather than
 *                 a generic JSON syntax error.
 *
 *    08/12/2019 - Removed redundant return statement, added on_rez event with call
 *                 to llResetScript(), and bumped version to 1.4.
 *
 * Licence:
 *
 *   MIT License
 *
 *   Copyright (c) 2020 John Parker
 *
 *   Permission is hereby granted, free of charge, to any person obtaining a copy
 *   of this software and associated documentation files (the "Software"), to deal
 *   in the Software without restriction, including without limitation the rights
 *   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 *   copies of the Software, and to permit persons to whom the Software is
 *   furnished to do so, subject to the following conditions:
 *   
 *   The above copyright notice and this permission notice shall be included in all
 *   copies or substantial portions of the Software.
 *   
 *   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 *   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 *   SOFTWARE.
**/

invalid_json( string err_msg )
{
    llRegionSay(
        DEBUG_CHANNEL,
        "Error: " + err_msg + " " +
        "Ensure that the necessary changes are implemented and try again."
    );
}

list get_button_list( list buttons )
{
    llSetMemoryLimit( llGetUsedMemory() + 4096 );
    
    integer len = llGetListLength( buttons );

    if( len == 0 ||
        ( len == 1 && llList2String( buttons, 0 ) == JSON_NULL ) )
        return [ " ", "OK", " " ];

    string button_str = llDumpList2String( buttons, "▫▫▫" );
    button_str = llDumpList2String( llParseStringKeepNulls( button_str, [ JSON_NULL ], [] ), " " );
    buttons = llParseStringKeepNulls( button_str, [ "▫▫▫" ], [] );

    integer mod = len % 3;
    
    if( mod != 0 )
    {
        if( mod == 1 )
        {
            buttons = llListInsertList( buttons, [ " " ], len - 1 );
            buttons = llListInsertList( buttons, [ " " ], len + 1 );
        }
        else if( mod == 2 )
        {
            buttons = llListInsertList( buttons, [ " " ], len - 1 );
        }
    }
    
    return buttons;
}

default
{
    state_entry()
    {
        llSetMemoryLimit( llGetUsedMemory() + 4096 );
    }
    
    on_rez( integer start_param )
    {
        llResetScript();
    }

    link_message( integer sender, integer value, string text, key id )
    {
        if( value != -39484225 )
            return;
        
        llSetMemoryLimit( llGetUsedMemory() + 4096 );

        key target = (key)llJsonGetValue( text, [ "target" ] );
        if( (string)target == JSON_INVALID )
        {
            invalid_json( "The required \"target\" parameter is missing." );
            return;
        }

        integer type = (integer)llJsonGetValue( text, [ "type" ] );
        
        string icon = llStringTrim( llJsonGetValue( text, [ "icon" ] ), STRING_TRIM );
        if( icon == JSON_INVALID || icon == "" )
        {
            icon = "icons/Info.png";
        }
        
        string title = llJsonGetValue( text, [ "title" ] );
        if( title == JSON_INVALID )
        {
            invalid_json( "The required \"title\" parameter is missing." );
            return;
        }
        
        string msg = llJsonGetValue( text, [ "message" ] );
        if( msg == JSON_INVALID )
        {
            invalid_json( "The required \"message\" parameter is missing." );
            return;
        }
        
        integer channel = (integer)((string)id);
        if( channel == 0 )
        {
            invalid_json( "The channel for the dialog is either missing or set to 0, which is not permitted." );
            return;
        }
        
        if( type == 0 )
        {
            string buttons = llJsonGetValue( text, [ "buttons" ] );
            if( buttons == JSON_INVALID )
            {
                invalid_json( "The required \"buttons\" parameter is missing." );
                return;
            }
            
            list btns = get_button_list( llJson2List( buttons ) );

            llDialog( target, "\n<icon>" + icon + "</icon> " + title + "\n\n" + msg, btns, channel );
        }
        else if( type == 1 )
        {
            llTextBox( target, "\n<icon>" + icon + "</icon> " + title + "\n\n" + msg, channel );
        }
        else
        {
            invalid_json( "The \"type\" parameter is invalid. This must be either TYPE_DIALOG (0) or TYPE_TEXTBOX (1)." );
        }
    }
}